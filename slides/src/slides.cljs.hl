(ns slides)

(defelem markdown-slide
  [attrs children]
  (section (merge attrs {:data-markdown ""})
           (script :type "text/template" children)))

(defn is-text?
  [children]
  (and (= (count children) 1)
       (not (= (.-nodeType (first children)) 1))))

(defelem slide-title
  [attrs children]
  (if (is-text? children)
    (add-attributes! (h2 (first children)) {:class "title"})
    children))

(defelem slide
  [{:keys [markdown title background transition] :as attrs} children]
  ;; TODO
  ;; - add data-background-repeat
  ;; - add data-background-size
  ;; - add class -> data-state
  ;; - abstract out this nonsense
  (let [attrs (dissoc attrs :markdown :title :background :transition)
        attrs (if background
                (assoc attrs :data-background background)
                attrs)
        attrs (if transition
                (assoc attrs :data-transition transition)
                attrs)
        children (if title
                   (concat [(slide-title title)] children)
                   children)]
    (if markdown
      (markdown-slide attrs children)
      (section attrs children))))

(defelem slideshow
  [{:keys [theme] :as attrs :or {theme "default"}} children]
  (add-initfn!
   (fn []   
     (.initialize
      js/Reveal
      (clj->js {:history true
                :dependencies
                [{:src "reveal.js/plugin/markdown/marked.js"}
                 {:src "reveal.js/plugin/markdown/markdown.js"}
                 {:src "reveal.js/plugin/highlight/highlight.js"
                  :async true
                  :callback (fn [] (.initHighlightingOnLoad js/hljs))}
                 {:src "reveal.js/plugin/notes/notes.js"
                  :async true}]}))))

  (div (-> attrs (dissoc :theme) (merge {:class "reveal"}))
       (link :rel "stylesheet"
             :href "reveal.js/css/reveal.css")
       (link :rel "stylesheet"
             :href (str "reveal.js/css/theme/" theme ".css")
             :id "theme")
       (link :rel "stylesheet"
             :href "reveal.js/plugin/highlight/github.min.css")
       (div :class "slides" children)))

(def t div)

(defn quicklist
  [& things]
  (ul
   (loop-tpl :bindings [x things]
             (li @x))))

(defelem notes
  [attrs children]
  (aside (merge attrs {:class "notes"}) children))

(defelem highlight
  [attrs children]
  (pre (code attrs children)))
